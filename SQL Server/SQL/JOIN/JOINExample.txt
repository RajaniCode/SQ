--JOIN Example
USE [master];
GO

IF EXISTS(SELECT name FROM sys.databases WHERE name = N'JoinExample')
DROP DATABASE JoinExample;
GO

CREATE DATABASE JoinExample;
GO

USE [JoinExample];
GO

IF OBJECT_ID('Table1', 'U') IS NOT NULL
DROP TABLE Table1
GO

CREATE TABLE Table1
(
	ID INT, Value VARCHAR(10)
)

INSERT INTO Table1 (ID, Value)
SELECT 1,'First'
UNION ALL
SELECT 2,'Second'
UNION ALL
SELECT 3,'Third'
UNION ALL
SELECT 4,'Fourth'
UNION ALL
SELECT 5,'Fifth'
GO

SELECT * FROM Table1;
GO

IF OBJECT_ID('Table2', 'U') IS NOT NULL
DROP TABLE Table2
GO

CREATE TABLE Table2
(
	ID INT, Value VARCHAR(10)
)

INSERT INTO Table2 (ID, Value)
SELECT 1,'I'
UNION ALL
SELECT 2,'II'
UNION ALL
SELECT 3,'III'
UNION ALL
SELECT 6,'VI'
UNION ALL
SELECT 7,'VII'
UNION ALL
SELECT 8,'VIII'
GO


SELECT * FROM Table2;
GO

/* INNER JOIN */
SELECT T1.*,T2.*
FROM Table1 T1
INNER JOIN Table2 T2 ON T1.ID = T2.ID
GO

/* LEFT JOIN */
SELECT T1.*,T2.*
FROM Table1 T1
LEFT JOIN Table2 T2 ON T1.ID = T2.ID
GO

/* RIGHT JOIN */
SELECT T1.*,T2.*
FROM Table1 T1
RIGHT JOIN Table2 T2 ON T1.ID = T2.ID
GO

/* OUTER JOIN */
SELECT T1.*,T2.*
FROM Table1 T1
FULL OUTER JOIN Table2 T2 ON T1.ID = T2.ID
GO

/* LEFT JOIN - WHERE NULL */
SELECT T1.*,T2.*
FROM Table1 T1
LEFT JOIN Table2 T2 ON T1.ID = T2.ID
WHERE T2.ID IS NULL
GO

/* RIGHT JOIN - WHERE NULL */
SELECT T1.*,T2.*
FROM Table1 T1
RIGHT JOIN Table2 T2 ON T1.ID = T2.ID
WHERE T1.ID IS NULL
GO

/* OUTER JOIN - WHERE NULL */
SELECT T1.*,T2.*
FROM Table1 T1
FULL OUTER JOIN Table2 T2 ON T1.ID = T2.ID
WHERE T1.ID IS NULL OR T2.ID IS NULL
GO

/* CROSS JOIN */
SELECT T1.*,T2.*
FROM Table1 T1
CROSS JOIN Table2 T2
GO

/* Inner and Outer Join as Self Join */
IF OBJECT_ID('Employee', 'U') IS NOT NULL
DROP TABLE Employee
GO

-- Create a Table
CREATE TABLE Employee
(
	EmployeeID INT PRIMARY KEY,
	Name NVARCHAR(50),
	ManagerID INT
)
GO

-- Insert Sample Data
INSERT INTO Employee
SELECT 1, 'Mike', 3
UNION ALL
SELECT 2, 'David', 3
UNION ALL
SELECT 3, 'Roger', NULL
UNION ALL
SELECT 4, 'Marry',2
UNION ALL
SELECT 5, 'Joseph',2
UNION ALL
SELECT 7, 'Ben',2
GO

-- Check the data
SELECT * FROM Employee;
GO

-- Inner Join as Self Join
SELECT E1.Name EmployeeName, E2.name AS ManagerName
FROM Employee E1
INNER JOIN Employee E2
ON E1.ManagerID = E2.EmployeeID
GO

-- Outer Join as Self Join
SELECT E1.Name EmployeeName, ISNULL(E2.name, 'Top Manager') AS ManagerName
FROM Employee E1
LEFT JOIN Employee E2
ON E1.ManagerID = E2.EmployeeID
GO
